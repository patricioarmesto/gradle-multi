plugins {
    id 'java-conventions'
    id 'application'
}

mainClassName = 'com.example.Hello'

application {
    mainModule = 'com.example'
    mainClass = mainClassName
}


dependencies {
    implementation project(':list')
    implementation project(':update')
}


tasks.register('uberJar', Jar) {
    archiveClassifier = 'uber'

    manifest {
        attributes 'Implementation-Title': 'Foobar',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Main-Class': mainClassName
    }
    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}